version 1.2

task somatic {
    meta {
        description: "Run Strelka somatic variant calling workflow"
        outputs: {
            strelka_output: "Directory containing Strelka somatic variant calls",
            log_file: "Log file from the Strelka workflow execution",
        }
    }

    parameter_meta {
        reference_fasta: "Reference genome in FASTA format"
        reference_fasta_index: "Index file for the reference genome FASTA"
        normal_bam: "Input BAM file with aligned reads for normal sample"
        tumor_bam: "Input BAM file with aligned reads for tumor sample"
        indel_candidates: "Optional VCF file with candidate indels, recommended to be generated by Manta"
        output_dir: "Directory to store Strelka output"
        exome: "Boolean indicating if the data is exome sequencing"
        rna: "Boolean indicating if the data is RNA sequencing"
        threads: "Number of threads to use"
        modify_disk_size_gb: "Additional disk size in GB to allocate"
    }

    input {
        File reference_fasta
        File reference_fasta_index
        File normal_bam
        File tumor_bam
        File? indel_candidates
        String output_dir = "strelka_somatic_output"
        Boolean exome = false
        Boolean rna = false
        Int threads = 4
        Int modify_disk_size_gb = 0
    }

    Int disk_size_gb = ceil(size(reference_fasta, "GiB") * 2)
        + ceil(size(normal_bam, "GiB"))
        + ceil(size(tumor_bam, "GiB"))
        + (
            if defined(indel_candidates)
            then ceil(size(indel_candidates, "GiB"))
            else 0
        )
        + 20
        + modify_disk_size_gb

    command <<<
        set -euo pipefail

        ref_fasta=~{basename(reference_fasta, ".gz")}
        gunzip -c "~{reference_fasta}" > "$ref_fasta" \
            || ln -sf "~{reference_fasta}" "$ref_fasta"
        ln -sf "~{reference_fasta_index}" "$ref_fasta.fai"

        configureStrelkaSomaticWorkflow.py \
            --referenceFasta "$ref_fasta" \
            --runDir "~{output_dir}" \
            --tumorBam "~{tumor_bam}" \
            --normalBam "~{normal_bam}" \
            ~{if (exome) then "--exome" else ""} \
            ~{if (rna) then "--rna" else ""} \
            ~{(
                if (defined(indel_candidates))
                then "--indelCandidates '~{indel_candidates}'"
                else ""
            )}

        
        "~{output_dir}/runWorkflow.py" -m local -j ~{threads}

        rm -rf "$ref_fasta"
    >>>

    output {
        Directory strelka_output = output_dir
        File log_file = "~{output_dir}/workspace/pyflow.data/logs/pyflow_log.txt"
    }

    requirements {
        container: "quay.io/biocontainers/strelka:2.9.10--hdfd78af_2"
        cpu: threads
        memory: "25 GB"
        disks: "~{disk_size_gb} GB"
    }
}

task germline {
    meta {
        description: "Run Strelka germline variant calling workflow"
        outputs: {
            strelka_output: "Directory containing Strelka germline variant calls",
            log_file: "Log file from the Strelka workflow execution",
        }
    }

    parameter_meta {
        reference_fasta: "Reference genome in FASTA format"
        reference_fasta_index: "Index file for the reference genome FASTA"
        bam: "Input BAM file with aligned reads"
        bam_index: "Index for input BAM file"
        output_dir: "Directory to store Strelka output"
        exome: "Boolean indicating if the data is exome sequencing"
        rna: "Boolean indicating if the data is RNA sequencing"
        threads: "Number of threads to use"
        modify_disk_size_gb: "Additional disk size in GB to allocate"
    }

    input {
        File reference_fasta
        File reference_fasta_index
        File bam
        File bam_index
        String output_dir = "strelka_germline_output"
        Boolean exome = false
        Boolean rna = false
        Int threads = 4
        Int modify_disk_size_gb = 0
    }

    Int disk_size_gb = ceil(size(reference_fasta, "GiB") * 2)
        + ceil(size(bam, "GiB"))
        + 20
        + modify_disk_size_gb

    command <<<
        set -euo pipefail

        ref_fasta=~{basename(reference_fasta, ".gz")}
        gunzip -c "~{reference_fasta}" > "$ref_fasta" \
            || ln -sf "~{reference_fasta}" "$ref_fasta"
        ln -sf "~{reference_fasta_index}" "$ref_fasta.fai"

        configureStrelkaGermlineWorkflow.py \
            --referenceFasta "$ref_fasta" \
            --runDir "~{output_dir}" \
            --bam "~{bam}" \
            ~{if (exome) then "--exome" else ""} \
            ~{if (rna) then "--rna" else ""}

        
        "~{output_dir}/runWorkflow.py" -m local -j ~{threads}

        rm -rf "$ref_fasta"
    >>>

    output {
        Directory strelka_output = output_dir
        File log_file = "~{output_dir}/workspace/pyflow.data/logs/pyflow_log.txt"
    }

    requirements {
        container: "quay.io/biocontainers/strelka:2.9.10--hdfd78af_2"
        cpu: threads
        memory: "25 GB"
        disks: "~{disk_size_gb} GB"
    }
}
